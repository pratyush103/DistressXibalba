task updateDataManifest {
    group "Xibalba"
    description "Generate core/assets/data/manifest.yaml"

    doLast {
        HashMap<String, Collection> mapped = new HashMap<>()
        File root = file("./core/assets/data")
        File manifest = file("${root}/manifest.yaml")

        manifest.write("")

        ["abilities", "defects", "enemies", "gods", "items", "traits"].each { category ->
            FileTree tree = fileTree(dir: "${root}/${category}")
            Collection collection = tree.collect { path -> "${root.toURI().relativize(path.toURI())}" }

            mapped.put(category, collection)
        }

        for (category in mapped) {
            manifest.append("${category.key}:\n")
            manifest.append(category.value.collect { path -> "  - ${path}" }.join("\n"))
            manifest.append("\n")
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {

    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = "alpha-${new Date().time}"

    ext {
        appName = "xibalba"
        gdxVersion = "1.9.8"
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        jcenter()
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-ai:1.8.1"
        compile "com.badlogicgames.ashley:ashley:1.7.3"
        compile "org.yaml:snakeyaml:1.16"
        compile "org.apache.commons:commons-lang3:3.4"
        compile "com.github.xaguzman:pathfinding:0.2.6"
        compile "com.esotericsoftware:kryo:4.0.0"
        compile "com.strongjoshua:libgdx-inGameConsole:0.5.3"
        compile "org.mini2Dx:universal-tween-engine:6.3.3"

        testCompile "junit:junit:4.12"
        testCompile "org.mockito:mockito-core:1.+"
        testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}